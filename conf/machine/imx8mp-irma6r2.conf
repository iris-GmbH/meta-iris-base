# SPDX-License-Identifier: MIT
# Copyright (C) 2022 iris-GmbH infrared & intelligent sensors

#@TYPE: Machine
#@NAME: NXP i.MX8MP for iris IRMA6 Release 2
#@SOC: i.MX8MP
#@DESCRIPTION: Machine configuration for iris IRMA6 Release 2 on i.MX8MP

MACHINEOVERRIDES =. "mx8mp:"

require conf/machine/include/imx-base.inc
require conf/machine/include/arm/armv8a/tune-cortexa53.inc

# Mainline BSP defaults to "generic" cortexa53 configuration,
# adjust it here to include crypto extension which enables
# inline NEON and FPU code generation
DEFAULTTUNE:use-mainline-bsp = "cortexa53-crypto"

# Mainline BSP doesn't support DDR4 so it must be set to nxp.
# Also this machine isn't supported by u-boot-fslc but imx8mp-evk.inc already
# set the bootloader to u-boot-imx instead when NXP BSP is used.
IMX_DEFAULT_BSP = "nxp"

# NXP BSP can consume proprietary jailhouse and Marvell drivers
# OP-TEE is also applicable to NXP BSP, mainline BSP seems not to have
# a full support for it yet.
MACHINE_FEATURES:append:use-nxp-bsp = " jailhouse mrvl8997"

KERNEL_DEVICETREE = "freescale/imx8mp-irma6r2.dtb"
KERNEL_DEVICETREE += "freescale/imx8mp-irma6r2-rev3.dtbo"

IMX_DEFAULT_BOOTLOADER:use-nxp-bsp = "u-boot-imx"
IMX_DEFAULT_BOOTLOADER:use-mainline-bsp = "u-boot-fslc"

UBOOT_CONFIG_BASENAME = "imx8mp-irma6r2"

# Set UBoot config for sd (set as default) and mfgtool builds, using the same defconfig
UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "imx8mp_irma6r2_defconfig,sdcard"
UBOOT_CONFIG[mfgtool] = "imx8mp_irma6r2_defconfig"
SPL_BINARY = "spl/u-boot-spl.bin"

ATF_PLATFORM = "imx8mp"

# Set DDR FIRMWARE
DDR_FIRMWARE_VERSION = "202006"
DDR_FIRMWARE_NAME = " \
    ddr4_dmem_1d_${DDR_FIRMWARE_VERSION}.bin \
    ddr4_imem_1d_${DDR_FIRMWARE_VERSION}.bin \
    ddr4_dmem_2d_${DDR_FIRMWARE_VERSION}.bin \
    ddr4_imem_2d_${DDR_FIRMWARE_VERSION}.bin \
"

# Set u-boot DTB
UBOOT_DTB_NAME = "imx8mp-irma6r2.dtb"

# Set imx-mkimage make target
IMXBOOT_TARGETS = "flash_ddr4_evk"

IMX_BOOT_SOC_TARGET = "iMX8MP"

# Set Serial console to UART 3
SERIAL_CONSOLES = "115200;ttymxc2"
ATF_BOOT_UART_BASE = "0x30880000"

LOADADDR = ""
UBOOT_SUFFIX = "bin"
UBOOT_MAKE_TARGET = "all"
IMX_BOOT_SEEK = "32"

OPTEE_BIN_EXT = "8mp"

KERNEL_IMAGETYPE:mx8mp-nxp-bsp = "Image.gz"
KERNEL_MODULE_AUTOLOAD += "powerfail-manager"

# set processor pcb hwrevision for swupdate
HW_VERSION = "6.2"


# FIXME RDPHOEN-1347: Disable loadable module support (CONFIG_MODULE) in linux-fslc's defconfig instead
MODULE_TARBALL_DEPLOY = "0"
