# SPDX-License-Identifier: MIT
# Copyright (C) 2021 iris-GmbH infrared & intelligent sensors

require conf/distro/poky.conf

DISTRO_NAME = "IRIS IRMA"
DISTRO_VERSION = "${IRMA_DISTRO_VERSION}"
unset DISTRO_CODENAME

# map IRIS_PRODUCT (set by kas to "irma6r1", "irma6r2" or "irma-matrix") to DISTROOVERRIDES
IRIS_PRODUCT ?= "none"
IRMA_HW_PLATFORM := "${@\
    '0601' if d.getVar('IRIS_PRODUCT', False) == 'irma6r1' else \
    '0602' if d.getVar('IRIS_PRODUCT', False) == 'irma6r2' else \
    '0501' if d.getVar('IRIS_PRODUCT', False) == 'matrix' else \
    '0000' \
}"
DISTROOVERRIDES .= ":poky-iris-${IRMA_HW_PLATFORM}"

# enable mdev
VIRTUAL-RUNTIME_dev_manager = "busybox-mdev"
VIRTUAL-RUNTIME_login_manager = "busybox"
VIRTUAL-RUNTIME_initscripts = "initscripts"
VIRTUAL-RUNTIME_keymaps = "keymaps"

# we only want to cherry-pick u-boot and kernel recipes from the adi upstream. ignore everything else
BBMASK += "meta-adi-adsp-sc5xx\/(?!(recipes-bsp\/u-boot)|(recipes-kernel\/linux)|(recipes-kernel\/linux-firmware)).+\/.+"
# mask cst-native_3.1.0, which will cause warnings due to non-existent binary
BBMASK += "meta-secure-imx/recipes-support/cst/cst-native_3.1.0.bb"

PACKAGE_CLASSES ?= "package_ipk"

# Set deploy dir for raw packages to tmpdir as we don't need them; reduces artifact size in gitlab CI
DEPLOY_DIR_IPK = "${TMPDIR}/ipk-deploy"
DEPLOY_DIR_RPM = "${TMPDIR}/rpm-deploy"
DEPLOY_DIR_DEB = "${TMPDIR}/deb-deploy"

USER_CLASSES ?= "buildstats"

PACKAGECONFIG:append:pn-qemu-native = " sdl"
PACKAGECONFIG:append:pn-nativesdk-qemu = " sdl"

# develop hab dir path will be overwritten during release
HAB_DIR ?= "${KAS_WORK_DIR}/meta-iris-base/dev_keys/hab"
HAB_DIR:mx93-nxp-bsp = "${KAS_WORK_DIR}/meta-iris-base/dev_keys/ahab"

SRKTAB ?= "${HAB_DIR}/crts/SRK_1_2_3_4_table.bin"
CSFK ?= "${HAB_DIR}/crts/CSF1_1_sha256_secp521r1_v3_usr_crt.pem"
SIGN_CERT ?= "${HAB_DIR}/crts/IMG1_1_sha256_secp521r1_v3_usr_crt.pem"
CRYPTO_HW_ACCEL ?= "CAAM"
BBMASK += "meta-secure-imx/recipes-bsp/u-boot/u-boot_%.bbappend"

# swupdate signing
SWUPDATE_DIR ?= "${KAS_WORK_DIR}/meta-iris-base/dev_keys/swupdate"
SWUPDATE_SIGNING ?= "CMS"
# develop keys will be overwritten during release
SWUPDATE_CA_CERT ?= "${SWUPDATE_DIR}/swupdate_snakeoil_ca.crt"
SWUPDATE_CMS_CERT ?= "${SWUPDATE_DIR}/swupdate_snakeoil.crt"
SWUPDATE_CMS_KEY ?= "${SWUPDATE_DIR}/swupdate_snakeoil.key"

# swupdate encryption
# used for signing the swupdate package
SWUPDATE_AES_FILE ?= "${SWUPDATE_DIR}/swupdate_snakeoil_encryption.key"
# used for adding the snakeoil key to rootfs via uuu
SWUPDATE_AES_SNAKEOIL_FILE ?= "${SWUPDATE_DIR}/swupdate_snakeoil_encryption.key"

# roothash signature key for the read only rootfs
SIGNING_KEYS_DIR ?= "${KAS_WORK_DIR}/meta-iris-base/dev_keys/signing"
ROOTHASH_SIGNING_PRIVATE_KEY ?= "${SIGNING_KEYS_DIR}/roothash-private-key.pem"
ROOTHASH_SIGNING_PUBLIC_KEY ?= "${SIGNING_KEYS_DIR}/roothash-public-key.pem"
ROOTHASH_DM_VERITY_SALT ?= "${SIGNING_KEYS_DIR}/roothash.salt"

# identity key and cert used for adding the snakeoil key to rootfs via uuu
IDENTITY_KEY_DIR ?= "${KAS_WORK_DIR}/meta-iris-base/dev_keys/identity"
IDENTITY_SNAKEOIL_KEY ?= "${IDENTITY_KEY_DIR}/sensor_identity_snakeoil.key"
IDENTITY_SNAKEOIL_CRT ?= "${IDENTITY_KEY_DIR}/sensor_identity_snakeoil.crt"

# certificate for encrypting the download archiv
DOWNLOAD_KEY_DIR ?= "${KAS_WORK_DIR}/meta-iris-base/dev_keys/download"
DOWNLOAD_CRT ?= "${DOWNLOAD_KEY_DIR}/download_snakeoil.crt"

IMAGE_FEATURES += "read-only-rootfs"
IMAGE_FEATURES:remove:poky-iris-0601 = "read-only-rootfs"
# force removal of user management tools
# since ROOTFS_POSTUNINSTALL_COMMAND does not actively uninstall them
FORCE_RO_REMOVE:poky-iris-0601 = "1"

# libgfortran throws an error because gcc is built without fortran support
# the following line (taken from poky/meta-poky/conf/local.conf.sample.extended)
# mutes the error
FORTRAN:forcevariable = ",fortran"

# mongoose is also provided by suitesparse. But we want the "real" mongoose recipe
PREFERRED_PROVIDER_mongoose = "mongoose"

# Multiple providers are available ... (libdevmapper-native, lvm2-native)
PREFERRED_RPROVIDER_libdevmapper-native = "libdevmapper-native"

# overwrite default distro features
DISTRO_FEATURES = "nfs ipv4 ipv6 multiarch"

# remove default backfilled features
DISTRO_FEATURES_BACKFILL_CONSIDERED = "pulseaudio gobject-introspection-data"
# ldconfig is used to rebuild the /etc/ld.so.cache when new libraries are introduced during runtime
# this is not needed and /etc/ld.so.cache is already populated by Yocto, disabling this saves ~250kB
DISTRO_FEATURES_BACKFILL_CONSIDERED:append:poky-iris-0601 =" ldconfig"

# Disable rtc in busybox
MACHINE_FEATURES_BACKFILL_CONSIDERED:poky-iris-0601 = "rtc"
